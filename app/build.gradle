apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.ledger.ledgerwallet"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 3
        versionName "1.0.0"
        //testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }


    productFlavors {
        beta {
            applicationId "com.ledger.ledgerwallet.beta"
            resValue "string", "app_name", "Ledger Wallet (Beta)"
            //buildConfigField "string", "LEDGER_ATTESTATION_PUBLIC_KEY", "04e69fd3c044865200e66f124b5ea237c918503931bee070edfcab79a00a25d6b5a09afbee902b4b763ecf1f9c25f82d6b0cf72bce3faf98523a1066948f1a395f"
        }

        mock {
            applicationId "com.ledger.ledgerwallet.beta"
            resValue "string", "app_name", "Ledger Wallet (For Mocked dongle)"
            //buildConfigField "string", "LEDGER_ATTESTATION_PUBLIC_KEY",  "0478c0837ded209265ea8131283585f71c5bddf7ffafe04ccddb8fe10b3edc7833d6dee70c3b9040e1a1a01c5cc04fcbf9b4de612e688d09245ef5f9135413cc1d"
        }

        production {
            //buildConfigField "string", "LEDGER_ATTESTATION_PUBLIC_KEY",  "04c370d4013107a98dfef01d6db5bb3419deb9299535f0be47f05939a78b314a3c29b51fcaa9b3d46fa382c995456af50cd57fb017c0ce05e4a31864a79b8fbfd6"
        }
    }

}

repositories {
    maven {
        url 'http://releases.marmeladburk.fidesmo.com'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.+'
    compile "org.scala-lang:scala-library:2.11.7"
    compile 'me.dm7.barcodescanner:zbar:1.6'
    compile 'com.android.support:support-v4:22.2.+'
    compile 'com.android.support:recyclerview-v7:22.2.+'
    compile 'com.koushikdutta.async:androidasync:2.1.+'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.madgag.spongycastle:pkix:1.51.0.0'
    compile 'com.madgag.spongycastle:pg:1.51.0.0'
    compile 'com.google.android.gms:play-services:3.1.+'
    compile 'com.netaporter:scala-uri_2.11:0.4.7'
    compile 'com.android.support:design:22.2.0'
    compile 'com.rockymadden.stringmetric:stringmetric-core_2.10:0.27.3'
    compile group: 'com.fidesmo', name: 'nordpol-android', version: '0.1.4', ext: 'aar', transitive: true
    compile 'com.google.guava:guava:19.0-rc1'

    androidTestCompile 'com.android.support:multidex-instrumentation:1.0.1'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.useCompileDaemon = true
}


afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$rootDir/main-dex-list.txt".toString()
    }
}
